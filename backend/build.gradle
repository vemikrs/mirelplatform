/* groovylint-disable-next-line CompileStatic */
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.github.ben-manes.versions" version "0.53.0"
}

group = 'jp.vemi'
// VERSION ファイルから動的に読み込み（Single Source of Truth）
def versionFile = file("${rootDir}/VERSION")
version = versionFile.exists() ? versionFile.text.trim() : '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://adoptopenjdk.jfrog.io/adoptopenjdk' }
    maven { url 'https://packages.adoptium.net/artifactory/api/maven/adoptium' }
}

// Spring AI BOM for dependency management
ext {
    springAiVersion = '1.1.2'
}

dependencyManagement {
    imports {
        // 順序重要: 後に宣言した BOM が優先される
        // spring-cloud-gcp が古い google-cloud-vertexai を引き込むため、spring-ai-bom を後に配置
        mavenBom "com.google.cloud:spring-cloud-gcp-dependencies:7.4.3"
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-aop:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-batch:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-cache:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-groovy-templates:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-mail:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-quartz:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.5.10'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.5.10'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server:6.4.0-RC1'
    implementation 'org.springframework.security:spring-security-oauth2-jose:6.4.0-RC1'
    implementation 'org.springframework.session:spring-session-data-redis'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    implementation 'com.h2database:h2:2.4.240'
    runtimeOnly 'org.postgresql:postgresql:42.7.9'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'org.apache.poi:poi:5.5.1'
    implementation('org.apache.poi:poi-ooxml:5.5.1') {
        exclude group: 'stax', module: 'stax-api'
    }
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'org.freemarker:freemarker:2.3.34'
    implementation 'com.codeborne:selenide:7.14.0'
    implementation 'org.hibernate.orm:hibernate-core:6.6.1.Final'
    // implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0' // Managed by Spring Boot
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0' // 3.2.xはHibernate 7.0向け、Spring Boot 3.xは3.1.xが必要
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    
    // Redis (分散レート制限・セッション管理)
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.5.10'
    implementation 'io.lettuce:lettuce-core:6.3.2.RELEASE'
    implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.14.1'
    
    // Azure Communication Services (メール送信)
    implementation 'com.azure:azure-communication-email:1.1.1'

    // Spring AI - Azure OpenAI (Mira AI アシスタント)
    implementation 'org.springframework.ai:spring-ai-starter-model-azure-openai'
    
    // Spring AI - OpenAI (GitHub Models 互換)
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'

    // Spring AI - Google Vertex AI (Gemini)
    implementation 'org.springframework.ai:spring-ai-starter-model-vertex-ai-gemini'
    implementation 'org.springframework.ai:spring-ai-starter-model-vertex-ai-embedding' // Added for Embedding (legacy naming?)
    // Vertex AI SDK - spring-cloud-gcp が古いバージョンをオーバーライドするのを防ぐため明示的に指定
    implementation 'com.google.cloud:google-cloud-vertexai:1.42.0'
    implementation 'com.google.api.grpc:proto-google-cloud-vertexai-v1:1.42.0'
    implementation 'com.google.api.grpc:grpc-google-cloud-vertexai-v1:1.42.0'

    // Spring AI - PGVector Store
    implementation 'org.springframework.ai:spring-ai-starter-vector-store-pgvector'

    // Spring AI - Tika Document Reader (PDF, Office, etc.)
    implementation 'org.springframework.ai:spring-ai-tika-document-reader'

    // JTokkit for Token Counting
    implementation 'com.knuddels:jtokkit:1.1.0'

    // Jackson CSV support (バージョンはSpring Bootの依存管理に従う)
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    
    // Thumbnailator for Image Compression
    implementation 'net.coobird:thumbnailator:0.4.21'
    
    // Markdown Parser for Contextual Chunking
    implementation 'org.commonmark:commonmark:0.27.1'
    
    // Google Cloud Storage (GCS)
    implementation 'com.google.cloud:spring-cloud-gcp-starter-storage'

    // AWS SDK for S3 (Cloudflare R2 互換)
    implementation platform('software.amazon.awssdk:bom:2.41.15')
    implementation 'software.amazon.awssdk:s3'
    
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-tomcat'
    // developmentOnly 'org.springframework.boot:spring-boot-devtools'  // Commented out for debugging
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.5.10'
    // WireMock for AI client testing
    testImplementation 'org.wiremock:wiremock-standalone:3.13.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    archiveBaseName = 'mirelplatform'
    archiveVersion = version
}

springBoot {
    mainClass = 'jp.vemi.mirel.MiplaApplication'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty 'file.encoding', 'UTF-8'
}

tasks.named('test') {
    useJUnitPlatform()
    maxHeapSize = '3g'
    jvmArgs '-XX:+UseG1GC'
    forkEvery = 50 // OOMを防止するため50テストごとにJVMをフォーク
}

// test {
//     enabled = false
// }

// application.yml 内の @...@ トークンを置換
// Spring Boot の ${} プレースホルダーと競合しないようにReplaceTokensを使用
import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching('config/application.yml') {
        filter(ReplaceTokens, tokens: [
            'project.version': project.version
        ])
    }
}
